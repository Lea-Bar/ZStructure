plugins {
    id 'java'
    id 'com.gradleup.shadow' version '8.3.8'
    id 'maven-publish'
}

group = 'fr.leabar'
version = '1.0'

repositories {
    mavenCentral()
    maven {
        name = "spigotmc-repo"
        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
}

dependencies {
    compileOnly("org.spigotmc:spigot-api:1.21.4-R0.1-SNAPSHOT")
    implementation("com.github.luben:zstd-jni:1.5.7-3")
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
}

shadowJar {
    configurations = [project.configurations.runtimeClasspath]
}

tasks.build.dependsOn tasks.shadowJar

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            groupId = 'fr.leabar'
            artifactId = 'ZStructure'
            version = this.version
        }
    }
}

wrapper {
    gradleVersion = "8.8"
    distributionType = Wrapper.DistributionType.ALL
}